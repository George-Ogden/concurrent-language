typedef Chain{T} (T, Chain{T});
typedef List{T} {Cons(T, List{T}) | Nil};

__<<-__ = (x: int, n: int) -> int {
    (x << n) - x
};

__::__ = (h: int, t: Chain{int}) -> Chain{int} {
    Chain{int}{(h, t)}
};

infinite_list = () -> Chain{int} {
    // modified from https://stackoverflow.com/a/8831937/12103577
    list_hash = (hash: int, n: int) -> Chain{int} {
        hash = (hash <<- 5) + n;
        hash :: list_hash(hash, n + 1)
    };
    list_hash(0, 0)
};

take{T} = (c: Chain{T}, n: int) -> List{T} {
    if (n <= 0) {
        Nil{T}{}
    } else {
        match (c) {
            Chain x: {
                h = x.0;
                t = x.1;
                Cons{T}{(h,take{T}(t, n-1))}
            }
        }
    }
};

main = (n: int) -> List{int} {
    take{int}(infinite_list(), n)
};
